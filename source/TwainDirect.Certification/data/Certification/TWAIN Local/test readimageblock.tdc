;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Test readImageBlock v1.0 24-Jun-2017
; arg:1 - image block to read
; arg:2 - metadata flag (true/false)
; returns: pass or fail
;
; Exercise the readImageBlock command.  We're going to do the following:
;
; - confirm that readImageBlock succeeds, and contains all required properties
;



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Display the banner...
;
echo
echo 'Test readImageBlock v1.0 24-Jun-2017'
set readimageblockresult 'pass'



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Call readImageBlock, and check the return properties...
;
call READIMAGEBLOCK '${arg:1}' '${arg:2}'



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Bye-bye...
;
echopassfail 'SUMMARY' '${get:readimageblockresult}'
return '${get:readimageblockresult}'



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; READIMAGEBLOCK
; returns pass or fail
;
:READIMAGEBLOCK
;
; Send the command...
readImageBlock '${arg:1}' '${arg:2}'
;
; Check the mandatory properties...
call CHECK 'kind'
call CHECK 'commandId'
call CHECK 'method' '==' 'readImageBlock'
call CHECK 'results'
call CHECK 'results.success' '==' 'true'
call CHECK 'results.session'
call CHECK 'results.session.sessionId' '!=' ''
call CHECK 'results.session.revision' '!=' ''
call CHECK 'results.session.state' '==' 'capturing'
;
; If we didn't ask for metadata, there'd better not be any...
if '${arg:2}' == 'true' goto READIMAGEBLOCK.CHECKMETADATA
if '${rg:results.metadata}' != '' goto READIMAGEBLOCK.FAIL
goto READIMAGEBLOCK.PASS
;
; We asked for metadata, so check it out...
:READIMAGEBLOCK.CHECKMETADATA
call CHECK 'results.metadata'
call CHECK 'results.metadata.status'
call CHECK 'results.metadata.status.success' == 'true'
call CHECK 'results.metadata.address'
if '${arg:1}' > '1' goto READIMAGEBLOCK.CHECKOTHERIMAGEBLOCKS
	call CHECK 'results.metadata.address.imageNumber' '==' '1'
	call CHECK 'results.metadata.address.imagePart' '==' '1'
	call CHECK 'results.metadata.address.sheetNumber' '==' '1'
goto READIMAGEBLOCK.SKIPOTHERIMAGEBLOCKS
:READIMAGEBLOCK.CHECKOTHERIMAGEBLOCKS
	call CHECK 'results.metadata.address.imageNumber' '!=' ''
	call CHECK 'results.metadata.address.imagePart' '!=' ''
	call CHECK 'results.metadata.address.sheetNumber' '!=' ''
:READIMAGEBLOCK.SKIPOTHERIMAGEBLOCKS
call CHECK 'results.metadata.address.moreParts' '==' 'false' 'true'
call CHECK 'results.metadata.address.source' '==' 'flatbed' 'feederFront' 'feederRear'
call CHECK 'results.metadata.address.streamName' '==' 'stream0'
call CHECK 'results.metadata.address.sourceName' '==' 'source0'
call CHECK 'results.metadata.address.pixelFormatName' '==' 'pixelFormat0'
call CHECK 'results.metadata.image'
call CHECK 'results.metadata.image.compression' == 'none' 'group4' 'jpeg'
call CHECK 'results.metadata.image.pixelFormat' == 'bw1' 'gray8' 'rgb24'
call CHECK 'results.metadata.image.pixelHeight' != ''
call CHECK 'results.metadata.image.pixelOffsetX' != ''
call CHECK 'results.metadata.image.pixelOffsetY' != ''
call CHECK 'results.metadata.image.pixelWidth' != ''
call CHECK 'results.metadata.image.resolution' != ''
call CHECK 'results.metadata.image.size' != ''
if '${get:readimageblockresult}' != 'pass' goto READIMAGEBLOCK.FAIL
;
; We're good...
:READIMAGEBLOCK.PASS
echopassfail 'readImageBlock call' 'pass'
return 'pass'
;
:READIMAGEBLOCK.FAIL
set readimageblockresult 'fail'
echopassfail 'readImageBlock call' 'fail'
return 'fail'



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; CHECK
; arg1 - property
; arg2 - if-statement operator (optional)
; arg3 - value to check for if arg2 is specified
; arg4 - value to check for if arg2 is specified
; arg5 - ...
; returns pass or fail
;
:CHECK
;
; Make sure the property exists...
if '${rjx:${arg:1}}' == '(null)' goto CHECK.MISSING
;
; if we have no operator, we're done...
if '${arg:2}' == '' goto CHECK.PASS
;
; Do the if-statement (init at 2, we'll start at 3), we are
; successful if we find an item, unsuccessful if we exhaust
; the list without finding a match...
set ifindex 2
:CHECK.NEXTIF
increment ifindex ${get:ifindex} 1
if '${get:ifindex}' > '${arg:#}' goto CHECK.FAILEDIF
if '${rj:${arg:1}}' '${arg:2}' '${arg:${get:ifindex}}' goto CHECK.PASS
goto CHECK.NEXTIF
;
; An if-statement failed...
:CHECK.FAILEDIF
echopassfail 'check ${arg:1}' 'fail (if '<${rj:${arg:1}}>' '${arg:2}' '<${arg:${get:ifindex}}>')'
set readimageblockresult 'fail'
return 'fail'
;
; The property is missing...
:CHECK.MISSING
echopassfail 'check ${arg:1}' 'fail (${arg1:} missing)'
set readimageblockresult 'fail'
return 'fail'
;
; We're good...
:CHECK.PASS
echopassfail 'check ${arg:1}' 'pass'
return 'PASS'
