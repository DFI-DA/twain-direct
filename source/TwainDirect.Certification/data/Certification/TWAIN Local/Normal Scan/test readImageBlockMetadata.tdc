;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Test readImageBlockMetadata
; Exercise the readImageBlockMetadata command.  We're going to do the following:
; - confirm that readImageBlockMetadata succeeds, and contains all required properties
;
; Arguments
; arg:1 - image block to read
; arg:2 - thumbnail flag (true/false)
; arg:3 - expected state
;
; Locals
; readimageblockmetadataresult - result of the test
;
; returns: pass or fail
;



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Display the banner...
;
echo
echo 'Test readImageBlockMetadata v1.0 17-Jul-2017'
set readimageblockmetadataresult 'pass'



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Call readImageBlockMetadata, and check the return properties...
;
call READIMAGEBLOCKMETADATA '${arg:1}' '${arg:2}' '${arg:3}'



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Bye-bye...
;
echopassfail 'SUMMARY' '${get:readimageblockmetadataresult}'
return '${get:readimageblockmetadataresult}'



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; READIMAGEBLOCKMETADATA
; arg:1 - image block to read
; arg:2 - thumbnail flag (true/false)
; arg:3 - expected state
; returns pass or fail
;
:READIMAGEBLOCKMETADATA
;
; Send the command...
readImageBlockMetadata '${arg:1}' '${arg:2}'
;
; Check the header...
call CHECKREPONSEHEADERS '${arg:2}'
;
; Check the mandatory properties...
run 'function CheckJsonProperty' 'readimageblockmetadataresult' 'kind' == 'twainlocalscanner'
run 'function CheckJsonProperty' 'readimageblockmetadataresult' 'commandId'
run 'function CheckJsonProperty' 'readimageblockmetadataresult' 'method' == 'readImageBlockMetadata'
run 'function CheckJsonProperty' 'readimageblockmetadataresult' 'results'
run 'function CheckJsonProperty' 'readimageblockmetadataresult' 'results.success' == 'true'
run 'function CheckJsonProperty' 'readimageblockmetadataresult' 'results.session'
run 'function CheckJsonProperty' 'readimageblockmetadataresult' 'results.session.sessionId' != ''
run 'function CheckJsonProperty' 'readimageblockmetadataresult' 'results.session.revision' != ''
run 'function CheckJsonProperty' 'readimageblockmetadataresult' 'results.session.state' == '${arg:3}'
run 'function CheckJsonProperty' 'readimageblockmetadataresult' 'results.metadata'
run 'function CheckJsonProperty' 'readimageblockmetadataresult' 'results.metadata.status'
run 'function CheckJsonProperty' 'readimageblockmetadataresult' 'results.metadata.status.success' == 'true'
run 'function CheckJsonProperty' 'readimageblockmetadataresult' 'results.metadata.address'
if '${arg:1}' > '1' goto READIMAGEBLOCKMETADATA.CHECKOTHERIMAGEBLOCKS
	run 'function CheckJsonProperty' 'readimageblockmetadataresult' 'results.metadata.address.imageNumber' '==' '1'
	run 'function CheckJsonProperty' 'readimageblockmetadataresult' 'results.metadata.address.imagePart' '==' '1'
	run 'function CheckJsonProperty' 'readimageblockmetadataresult' 'results.metadata.address.sheetNumber' '==' '1'
	goto READIMAGEBLOCKMETADATA.SKIPOTHERIMAGEBLOCKS
:READIMAGEBLOCKMETADATA.CHECKOTHERIMAGEBLOCKS
	run 'function CheckJsonProperty' 'readimageblockmetadataresult' 'results.metadata.address.imageNumber' '!=' ''
	run 'function CheckJsonProperty' 'readimageblockmetadataresult' 'results.metadata.address.imagePart' '!=' ''
	run 'function CheckJsonProperty' 'readimageblockmetadataresult' 'results.metadata.address.sheetNumber' '!=' ''
:READIMAGEBLOCKMETADATA.SKIPOTHERIMAGEBLOCKS
run 'function CheckJsonProperty' 'readimageblockmetadataresult' 'results.metadata.address.moreParts' '==' 'false' 'true'
run 'function CheckJsonProperty' 'readimageblockmetadataresult' 'results.metadata.address.source' '==' 'flatbed' 'feederFront' 'feederRear'
run 'function CheckJsonProperty' 'readimageblockmetadataresult' 'results.metadata.address.streamName' '==' 'stream0'
run 'function CheckJsonProperty' 'readimageblockmetadataresult' 'results.metadata.address.sourceName' '==' 'source0'
run 'function CheckJsonProperty' 'readimageblockmetadataresult' 'results.metadata.address.pixelFormatName' '==' 'pixelFormat0'
run 'function CheckJsonProperty' 'readimageblockmetadataresult' 'results.metadata.image'
run 'function CheckJsonProperty' 'readimageblockmetadataresult' 'results.metadata.image.compression' == 'none' 'group4' 'jpeg'
run 'function CheckJsonProperty' 'readimageblockmetadataresult' 'results.metadata.image.pixelFormat' == 'bw1' 'gray8' 'rgb24'
run 'function CheckJsonProperty' 'readimageblockmetadataresult' 'results.metadata.image.pixelHeight' != ''
run 'function CheckJsonProperty' 'readimageblockmetadataresult' 'results.metadata.image.pixelOffsetX' != ''
run 'function CheckJsonProperty' 'readimageblockmetadataresult' 'results.metadata.image.pixelOffsetY' != ''
run 'function CheckJsonProperty' 'readimageblockmetadataresult' 'results.metadata.image.pixelWidth' != ''
run 'function CheckJsonProperty' 'readimageblockmetadataresult' 'results.metadata.image.resolution' != ''
run 'function CheckJsonProperty' 'readimageblockmetadataresult' 'results.metadata.image.size' != ''
if '${get:readimageblockmetadataresult}' != 'pass' goto READIMAGEBLOCKMETADATA.FAIL
;
; We're good...
echopassfail 'releaseImageBlockMetadata call' 'pass'
return 'pass'
;
:READIMAGEBLOCKMETADATA.FAIL
set readimageblockmetadataresult 'fail'
echopassfail 'releaseImageBlockMetadata call' 'fail'
return 'fail'



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; CHECKREPONSEHEADERS
; arg:1 - thumbnail flag (true/false)
; returns pass or fail
;
:CHECKREPONSEHEADERS
;
; Handle a normal header...
if '${arg:1}' == 'true' goto CHECKREPONSEHEADERS.MULTIPART
run 'function CheckHeader' 'readimageblockmetadataresult' 'Content-Type' ~contains 'application/json' 'charset=utf-8'
run 'function CheckHeader' 'readimageblockmetadataresult' 'Content-Length' == '${rdata:#}'
return '${get:readimageblockmetadataresult}'
;
; Handle a multipart header...
:CHECKREPONSEHEADERS.MULTIPART
call CHECKJSONHEADER 'content-type' '~contains' 'application/json'
call CHECKJSONHEADER 'content-type' '~contains' 'charset=utf-8'
call CHECKJSONHEADER 'content-length' '==' '${rj:#}'
return '${get:readimageblockmetadataresult}'



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; CHECKJSONHEADER
; arg:1 - header name
; arg:2 - if-statement operation
; arg:3 - required value
; returns pass or fail
;
:CHECKJSONHEADER
;
; Look for the header key...
set iHeader 0
:CHECKJSONHEADER.NEXT
	;
	; We've run out of keys to check...
	if '${get:iHeader}' >= '${hdrjsonkey:#}' goto CHECKJSONHEADER.FAIL
	;
	; We have a match (note that we're insensitive to case)...
	if '${hdrjsonkey:${get:iHeader}}' ~~ '${arg:1}' goto CHECKJSONHEADER.FOUND
	;
	; Next key...
	increment 'iHeader' '${get:iHeader}' '1'
goto CHECKJSONHEADER.NEXT
;
; We found something, so check for a match...
:CHECKJSONHEADER.FOUND
if '${hdrjsonvalue:${get:iHeader}}' '${arg:2}' '${arg:3}' goto CHECKJSONHEADER.PASS
;
; No joy...
:CHECKJSONHEADER.FAIL
echopassfail 'check json header ${arg:1}' 'fail (<${hdrjsonvalue:${get:iHeader}}> ${arg:2} <${arg:3}>)'
set readimageblockmetadataresult 'fail'
return 'fail'
;
; We're good...
:CHECKJSONHEADER.PASS
echopassfail 'check json header ${arg:1}' 'pass'
return 'pass'
